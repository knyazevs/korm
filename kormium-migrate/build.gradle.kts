plugins {
    kotlin("multiplatform")
    id("com.android.kotlin.multiplatform.library")
}

repositories {
    google()
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.jetbrains.space/public/p/ktor/eap")
    }
}

kotlin {
    jvmToolchain(21)

    jvm {
        testRuns["test"].executionTask.configure {
            useJUnitPlatform()
        }
    }

    // Compose Multiplatform targets (AGP KMP library plugin's androidLibrary DSL).
    android {
        namespace = "io.github.kormium.migrate"
        compileSdk = 36
        minSdk = 24
    }
    iosX64()
    iosArm64()
    iosSimulatorArm64()

    linuxX64()
    macosX64()
    macosArm64()
    // mingwX64() // deferred — see the publishing plan

    applyDefaultHierarchyTemplate()

    sourceSets {
        val commonMain by getting {
            dependencies {
                // The migration runner is built on core's Database/SqlExecutor/Dialect seam, which
                // appear in the public `migrate` / `Migration` signatures, so :kormium-core is api.
                api(project(":kormium-core"))
                // Instant for the migration journal's applied_at timestamp (internal use only).
                implementation("org.jetbrains.kotlinx:kotlinx-datetime:0.6.2")
            }
        }
        val commonTest by getting {
            dependencies {
                implementation(kotlin("test"))
                // End-to-end runner tests execute against a real SQLite :memory: database, the
                // same approach kormium-observe uses for SqliteObserveTest.
                implementation(project(":kormium-sqlite"))
            }
        }
    }
}
